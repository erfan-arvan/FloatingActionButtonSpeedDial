/*
 * Copyright 2022 Roberto Leinardi.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
    id 'speeddial.android-sample-conventions'
}

android {
    namespace = "com.leinardi.android.speeddial.sample"
    defaultConfig {
        applicationId config.android.applicationId
        archivesBaseName = "floating_action_button_speed_dial"
        versionName config.android.version_name_sample
        minSdk config.android.minSdkCompose

        vectorDrawables {
            useSupportLibrary true
        }
    }

    def isKeystoreInfoAvailable = file(KEYSTORE_PATH).exists() && file(KEYSTORE_PROPERTIES_PATH).exists()

    signingConfigs {
        if (isKeystoreInfoAvailable) {
            release {
                Properties props = new Properties()
                props.load(new FileInputStream(file(KEYSTORE_PROPERTIES_PATH)))
                storeFile file(KEYSTORE_PATH)
                storePassword props['store.password']
                keyAlias props['key.alias']
                keyPassword props['key.password']
            }
        }
    }

    buildTypes {
        if (isKeystoreInfoAvailable) {
            release {
                minifyEnabled true
                shrinkResources true
                proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
                signingConfig signingConfigs.release
            }
        }
        debug {
            applicationIdSuffix '.debug'
            versionNameSuffix '-DEBUG'
        }
    }
}

dependencies {
    implementation projects.libraryView
    //    implementation "com.leinardi.android:speed-dial:${config.android.version_name_view}"
    implementation libs.androidx.core.ktx
    implementation libs.androidx.constraintlayout
    implementation libs.androidx.recyclerview

    implementation projects.libraryCompose
    //    implementation "com.leinardi.android:speed-dial.compose:${config.android.version_name_compose}"
    implementation libs.accompanist.systemuicontroller
    implementation libs.androidx.activity.compose
    //    implementation libs.androidx.compose.material
    implementation libs.androidx.compose.runtime
    implementation libs.androidx.compose.tooling
    implementation libs.androidx.compose.ui
    implementation libs.coroutines.core
    implementation libs.coroutines.android
}
